apply plugin: "com.android.application"

ext.trc_version = "0.5.1"

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("commons-io:commons-io") with module("com.github.Bible-Translation-Tools:BTTConverter:$trc_version")
        substitute module("org.json:json") with module("com.github.Bible-Translation-Tools:BTTConverter:$trc_version")
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "bible.translationtools.converter"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 22
        versionName "1.6.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        if(project.hasProperty('keystorePath')
                && project.hasProperty('storePass')
                && project.hasProperty('keyPass')) {
            release {
                storeFile file(keystorePath)
                storePassword storePass
                keyAlias "translationRecorderAndroidKey"
                keyPassword keyPass
            }
        } else {
            release {}
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildToolsVersion "28.0.3"
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation "com.github.Bible-Translation-Tools:BTTConverter:$trc_version"
    implementation "com.toptoche.searchablespinner:searchablespinnerlibrary:1.3.1"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    testImplementation "junit:junit:4.12"
}
